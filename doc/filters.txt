Conditionals:

if <condition>                Executes the contents of the block if <condition> is met

else if <condition>           Executes the contents of the block if <condition> is met
                              and a previous if or else if condition was not already met
elseif, elsif, elif           Alternative spellings of else if

or if <condition>             Executes the contents of the block if <condition> is met
                              or the previous if or else if condition was met
orif                          Alternative spelling of or if

else                          Executes the contents of the block if no previous if or else if condition was already met

end if                        Ends the block started by if
endif, fi                     Alternative spellings of endif

Suffixing a 'n' to if, or if or else if inverts the sense of the condition.



Conditions:

Testing properties of the tweet being filtered:
tweet.<variable>              Tweet variable
retweet.<variable>            If this tweet is a retweet, test a property of the original tweet,
                              otherwise test the tweet as normal

Testing properties of user accounts associated with the tweet being filtered:
user.<variable>               Tweet author
retweetuser.<variable>        If this tweet is a retweet, test a property of the original tweet's author,
                              otherwise test the tweet's author as normal
userrecipient.<variable>      If this tweet is a DM, test a property of the DM's recipient,
                              otherwise test the tweet's author as normal
anyuser.<variable>            Matches if one or more of user, retweetuser and userrecipient would match.
accountuser.<variable>        User of the account that the tweet is arriving on.
                              If not present, all properties will have the value of the empty string.

Tweet Condition Variables:

.flags <FLAGS>                Tests the current tweet's flags
                              where FLAGS is a set of zero or more flag letters, without intermediary whitespace
                              See tweetflags.txt for flag descriptions.
    +FLAGS                    At least one of FLAGS must be set (this is the default, if no symbol is used)
    =FLAGS                    All of FLAGS must be set
    -FLAGS                    None of FLAGS must be set
    /FLAGS                    At least one of FLAGS must be unset

The following condition variables accept a PCRE regular expression: regex, which is used to match a text value
.text <regex>                 Tweet text
.source <regex>               The tweet source string describes which app/client was used to post the tweet.
                              It is usually a HTML link.


User Condition Variables:

The following condition variables accept a PCRE regular expression: regex, which is used to match a text value
.name <regex>                 User's long name (not the @ screen name)
.screenname <regex>           User's screen name, excluding the @ symbol
.sname <regex>                Alternate spelling of .screenname
.description <regex>          User's description
.desc <regex>                 Alternate spelling of .description
.location <regex>             User's location
.loc <regex>                  Alternate spelling of .location


Actions:

set tweet.flags <FLAGS>       Sets or unsets one or more tweet flags
                              where FLAGS is a set of zero or more flag letters, without intermediary whitespace
                              See tweetflags.txt for flag descriptions.
    +FLAGS                    These FLAGS will be SET (this is the default, if no symbol is used)
    -FLAGS                    These FLAGS must be UNSET
                              Note that only some flags can be changed in this manner.
                              The flags which can currently be change include those in the set: hnpruH

panel add <PANEL>             Add tweet to manual panel: PANEL
                              If the panel does not exist it will be created on demand

panel remove <PANEL>          Remove tweet from manual panel: PANEL
                              If the panel does not exist this has no effect

Comments:

Lines which begins with a '#' character are ignored.


Example Filters:

Highlight all tweets arriving from user "Alice"
or from any user with "bob" (any case) in their screen name.

if user.screenname ^Alice$
or if user.sname (?i)bob
  set tweet.flags +H
end if


Hide image thumbnails for all tweets with images which arrive on accounts other than "Carol",
or which Twitter has marked as "possibly sensitive"

if tweet.flags +I
  ifn accountuser.sname ^Carol$
  orif tweet.flags +P
    set tweet.flags +p
  fi
fi


Add tweets retweeted by user "Dave" or users whose screen names start with "Eve",
and which contain the text "#hashtag", to panel "#hashtag stuff"

if tweet.flags +R
  if user.sname ^Dave$|^Eve
    if tweet.text #hashtag
      panel add #hashtag stuff
    fi
  fi
fi


Mark all tweets posted by, retweeted by or DMd to/from user "Fred" as read

if anyuser.sname ^Fred$
  set tweet.flags -u+r
endif


Hide and clear unread flag for all tweets coming from twitter client "spamapp", which are not mentions

if tweet.source ^spamapp$
  if tweet.flags -M
    set tweet.flags +h-u
  fi
fi
